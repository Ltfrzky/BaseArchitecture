plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def baseEnvPropertiesFile = rootProject.file("base-env.properties")
def baseEnvProp = new Properties()
baseEnvProp.load(new FileInputStream(baseEnvPropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.lutfi.basearchitecture"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "${baseEnvProp["BASE_URL"]}"
        }

        debug {
            multiDexEnabled true
            debuggable true
            buildConfigField "String", "BASE_URL", "${baseEnvProp["BASE_URL_DEBUG"]}"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation project(':core')
    implementation project(':feature-main')
    rootProject.presentationDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
}

kapt {
    correctErrorTypes true
}